initialize() {
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(dimensionality="xy");
	
	defaults = Dictionary(
		"seed", getSeed(),
		"SD", 0.95, // sigma_D, dispersal distance
		"SX", 0.3, // sigma_X, interaction distance for measuring local density
		"SM", 0.5, // sigma_M, mate choice distance
		"K", 25, // carrying capacity per unit area
		"A", 0.25, // constant in B-H form
		"B", 0.95, // another constant
		"ALPHA", 0.25, // another parameter
		"GAMMA_0", 0.3, // fecundity at low density
		"WIDTH", 36.0, // width of the simulated area
		"HEIGHT", 24.0, // height of the simulated area
		"RUNTIME", 200, // total number of ticks to run the simulation for
		"L", 1e8, // genome length
		"R", 1e-8, // recombination rate
		"MU", 0 // mutation rate
		);
	
	// Set up parameters with a user-defined function
	setupParams(defaults);
	
	// Set up constants that depend on externally defined parameters
	
	defineGlobal("PARAMS", defaults);
	
	setSeed(seed);
	
	initializeMutationRate(MU);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(R);
	
	// spatial interaction for local density measure
	initializeInteractionType(1, "xy", reciprocal=T, maxDistance=3 * SX);
	i1.setInteractionFunction("n", 1, SX);
	// mate choice
	initializeInteractionType(2, "xy", reciprocal=T, maxDistance=3 * SM);
	i2.setInteractionFunction("n", 1, SM);
}

1 first() {
	sim.addSubpop("p1", asInteger(K * WIDTH * HEIGHT));
	p1.setSpatialBounds(c(0, 0, WIDTH, HEIGHT));
	p1.individuals.setSpatialPosition(p1.pointUniform(p1.individualCount));
	community.rescheduleScriptBlock(s1, ticks=RUNTIME);
	p1.individuals.tag = 0;
}

first() {
	// preparation for the reproduction() callback
	i2.evaluate(p1);
}

reproduction(){
	mate = i2.drawByStrength(individual, 1);
	if (mate.size() & individual.tag > 0)
		subpop.addCrossed(individual, mate, count=individual.tag);
}

early() {
	// Disperse offspring
	offspring = p1.subsetIndividuals(maxAge = 0);
	pos = offspring.spatialPosition;
	pos = p1.pointDeviated(offspring.size(), pos, "reprising", INF, "n", SD);
	offspring.setSpatialPosition(pos);
	
	// Measure local density and use it for density regulation
	i1.evaluate(p1);
	inds = p1.individuals;
	density = i1.localPopulationDensity(inds);
	Fn = ALPHA * ((1 + A) / (1 + A * density/K) - 1);
	mean_fecundity = GAMMA_0 / (1 + B * density/K);
	inds.tag = rpois(length(inds), mean_fecundity);
	p1.individuals.fitnessScaling = (Fn + 1) / (1 + mean_fecundity);
}

late() {
	if (p1.individualCount == 0) {
		catn("Population went extinct! Ending the simulation.");
		sim.simulationFinished();
	}
}

s1 late() {
	catn("End of simulation (run time reached)");
	sim.simulationFinished();
}

function (void)setupParams(object<Dictionary>$ defaults)
{
	if (!exists("PARAMFILE")) defineConstant("PARAMFILE", "./params.json");
	if (!exists("OUTDIR")) defineConstant("OUTDIR", ".");
	defaults.addKeysAndValuesFrom(Dictionary("PARAMFILE", PARAMFILE, "OUTDIR", OUTDIR));
	
	if (fileExists(PARAMFILE)){
		local_defaults = Dictionary(paste(readFile(PARAMFILE)));
		defaults.addKeysAndValuesFrom(local_defaults);
		defaults.setValue("read_from_paramfile", PARAMFILE);
	}
	
	defaults.setValue("OUTBASE", OUTDIR + "/out_" +	defaults.getValue("seed"));
	defaults.setValue("OUTPATH", defaults.getValue("OUTBASE") + ".trees");
	
	for (k in defaults.allKeys) {
		if (!exists(k)) {
			defineConstant(k, defaults.getValue(k));
		}
		else {
			defaults.setValue(k, executeLambda(paste(c(k, ";"), sep='')));
		}
	}
	
	// print out default values
	catn("===========================");
	catn("Model constants: " + defaults.serialize());
	catn("===========================");
}

late() {
	i1.evaluate(p1);
	inds = p1.individuals;
	density = i1.localPopulationDensity(inds);
	inds.color = colors((density - min(density))/(max(density) - min(density)), "jet");
}
